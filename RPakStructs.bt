// structs for asset "head" sections
// note: most of these are specifically for the season 3 version of the game

typedef uint64 RPakGuid<format=hex>;
typedef RPakGuid RPakGuidRef<fgcolor=cYellow>;

/*struct Vector3
{
    float x,y,z;
};*/

#include "MiscStructs.bt"

struct PakHdr_t
{
	char id[4] <fgcolor=cLtGreen>;
	uint16 version <fgcolor=cLtBlue>;

    // these are technically the same var but meh
	char flags <fgcolor=cPurple>;
	char IsCompressed <fgcolor=cGreen>;

	FILETIME TimeCreated <name="File Creation Time">;
    uint64 yes <format=hex>;

    if(FILE_VERSION >= 7)
	    uint64 cmpSize;

    if(FILE_VERSION == 8)
	    uint64 EmbeddedStarpakOffset;

    if(FILE_VERSION >= 7)
	    uint64 Padding;

	uint64 dcmpSize;

    if(FILE_VERSION == 6)
	    uint64 Padding;

    if(FILE_VERSION == 8)
	    uint64 EmbeddedStarpakSize;
	uint64 Padding2;

    if(FILE_VERSION == 6)
	    uint32 lenStarpakPaths;
    else
        uint16 lenStarpakPaths;

    if(FILE_VERSION == 8)
	    uint16 lenOptStarpakPaths;

    if(FILE_VERSION == 6)
    {
	    uint32 VirtualSegmentCount;
        uint32 PageCount;
    }
    else
    {
        uint16 VirtualSegmentCount;			// * 0x10
	    uint16 PageCount;		// * 0xC
    }

     if(FILE_VERSION >= 7)
        uint16 PatchIndex;

    if(FILE_VERSION == 8)
        uint16 align;
    

	uint32 DescriptorCount;
	uint32 AssetEntryCount;
	uint32 GuidDescriptorCount;
	uint32 FileRelationCount;

    uint32 ExternalAssetCount;
    uint32 ExternalAssetSize;

    if(FILE_VERSION == 6)
        uint32 what;

    if(FILE_VERSION == 8)
    {
	    char Unk[0x8];
        uint32 UnkCount3;
        char Unk1[0x8];
    }
};

struct RpakPatchHeader
{
	uint32 dataSize;				// Total size of the patch edit stream data (Following all data blocks)
	uint32 patchPageIdx;			// Index into RpakVirtualSegmentBlock[], this entire virtual block is read FIRST, before first asset
};

struct RpakPatchCompressPair
{
	uint64 cmpSize;
	uint64 dcmpSize;
};

struct RpakVirtualSegment
{
	uint32 flags;
	uint32 align;
	uint64 size;
};

struct RpakPageInfo
{
	uint32 segIdx;
	uint32 align;					// Unknown right now
	uint32 dataSize;				// Total size of the block
};

struct RpakDescriptor
{
	uint32 Index;
	uint32 Offset;
};

struct RpakSegmentBlock
{
	uint64 Offset;
	uint64 Size;
};

struct RpakAssetEntry
{
	uint64 NameHash <format=hex, fgcolor=cLtGreen>;
	uint64 Padding;

	int SubHeaderPageIndex;
	int SubHeaderPageOffset;
	int RawDataPageIndex;
	int RawDataPageOffset;

	int64 StarpakOffset;	

    if(FILE_VERSION == 8)			
	    int64 OptionalStarpakOffset;		

	uint16 HighestPageNum; // pageEnd
	uint16 Un2;

	uint32 RelationsStartIndex;		

	uint32 UsesStartIndex; // page index for the list of the assets that this asset uses
	uint32 RelationsCount;  // number of relations
	uint32 UsesCount;      // number of assets that are used/referenced by guid

	uint32 SubHeaderSize;
	uint32 Version;
	char Magic[4] <fgcolor=cWhite>;
};

struct RPakPtr
{
    uint32_t Index;
    uint32_t Offset;
};

struct RpakDescPointer
{
    uint32 Index  <fgcolor=cRed>;
    uint32 Offset <fgcolor=cLtBlue>;
};

enum SegmentFlags
{
    CPU = (1 << 0), // 0x1
    
    SERVER_ONLY = (1 << 5), // 0x20
    CLIENT_ONLY = (1 << 6), // 0x40
    DEV_ONLY = (1 << 7), // 0x80
};

string ReadSegmentFlags(uint flags)
{
    string value = "";

    if(flags == 0)
        return "HEAD";

    int i = 0;
    int flag = 0;

    int flagsFound = 0;

    for(i = 0; i < 32; i++)
    {
        flag = flags & (1 << i);

        if(flag != 0)
        {
            flagsFound++;
        }
    }

    if((flags & 1) != 1)
        value += "HEAD | ";

    int flagCounter = 0;
    for(i = 0; i < 32; i++)
    {
        flag = flags & (1 << i);

        switch(flag)
        {
        case 0:
            break;
        case 0x1:
            value += "CPU";
            break;
        case 0x2:
            value += "TEMP";
            break;
        case 0x20:
            value += "SERVER";
            break;
        case 0x40:
            value += "CLIENT";
            break;
        case 0x80:
            value += "DEV";
            break;
        default:
            value += Str("UNK_%x", flag);
            break;
        }

        if(flag != 0)
        {
            flagCounter++;
            if(flagCounter < flagsFound)
            {
                value += " | ";
            }
        }
    }

    return value;
}

////////////
// Assets //
////////////

//
// MATERIALS 
//

enum <byte> MaterialShaderType_t
{
  RGDU = 0x0,
  RGDP = 0x1,
  RGDC = 0x2,
  SKNU = 0x3,
  SKNP = 0x4,
  SKNC = 0x5,
  WLDU = 0x6,
  WLDC = 0x7,
  PTCU = 0x8,
  PTCS = 0x9,
};

struct UnknownMaterialSectionV15
{
    // required but seems to follow a pattern. maybe related to "Unknown2" above?
    // nulling these bytes makes the material stop drawing entirely
    uint32_t unknown[8];
    
    // for more details see the 'UnknownMaterialSectionV12' struct.
    int unkFlags; // this changes sometimes.
	short visibilityFlags; // different render settings, such as opacity and transparency.
	short faceFlags; // how the face is drawn, culling, wireframe, etc.
    
    __int64 reserved; // probably
};

struct UnknownMaterialSectionV12
{
	// not sure how these work but 0xF0 -> 0x00 toggles them off and vice versa.
	// they seem to affect various rendering filters, said filters might actually be the used shaders.
	// the duplicate one is likely for streamed textures.
	uint32_t unkRenderLighting;
	uint32_t unkRenderAliasing;
	uint32_t unkRenderDoF;
	uint32_t unkRenderUnknown;

	int unkFlags; // this changes sometimes.
	short visibilityFlags; // different render settings, such as opacity and transparency.
	short faceFlags; // how the face is drawn, culling, wireframe, etc.

	__int64 reserved; // probably
};

struct MaterialHeaderV12
{
	__int64 reservedVtbl; // Gets set to CMaterialGlue vtbl ptr
	char padding[8]; // unused

	uint64_t guid <read=Str("%s - %LX", "matl", this)>; // guid of this material asset

	RPakPtr pName <read=ReadString(pageoffsets[this.Index] + this.Offset)>; // pointer to partial asset path
	RPakPtr pSurfaceProp <read=ReadString(pageoffsets[this.Index] + this.Offset)>; // pointer to surfaceprop (as defined in surfaceproperties.txt)
	RPakPtr pSurfaceProp2 <read=ReadString(pageoffsets[this.Index] + this.Offset)>; // pointer to surfaceprop2 

	// IDX 1: DepthShadow
	// IDX 2: DepthPrepass
	// IDX 3: DepthVSM
	// IDX 4: ColPass

	uint64_t guidRefs[4] <read=Str("%s - %LX", "matl", this)>; // Required to have proper textures.

	// these blocks relate to different render filters and flags. still not well understood.
	UnknownMaterialSectionV12 unkSections[2];

	uint64_t shadersetGuid <read=Str("%s - %LX", "shds", this)>; // guid of the shaderset asset that this material uses

	RPakPtr pTextureHandles; // TextureGUID Map
	RPakPtr pStreamingTextureHandles; // reserved slot for texture guids which have streamed mip levels
	short streamingTextureCount; // reserved slot for the number of textures with streamed mip levels

	int flags <read=Str("0x%LX", this)>; // related to cpu data

	short unknown2; // reserved texture count?

	__int64 reserved; // nothing is ever placed here (as I've seen so far), probably reserved for stuff on load.

	// seems to be 0xFBA63181 for loadscreens
	int unknown3; // name carried over from apex struct.
	int unknown4; // name carried over from apex struct.

	__int64 flags2 <read=Str("0x%LX", this)>;
	
	short width;
	short height;

	int unknown1; // reserved texture count?
};

struct MaterialHeaderV15
{
	__int64 reservedVtbl; // Gets set to CMaterialGlue vtbl ptr
	char padding[8]; // unused

	uint64_t guid <read=Str("%s - %LX", "matl", this)>; // guid of this material asset

	RPakPtr pName <read=ReadString(pageoffsets[this.Index] + this.Offset)>; // pointer to partial asset path
	RPakPtr pSurfaceProp <read=ReadString(pageoffsets[this.Index] + this.Offset)>; // pointer to surfaceprop (as defined in surfaceproperties.txt)
	RPakPtr pSurfaceProp2 <read=ReadString(pageoffsets[this.Index] + this.Offset)>; // pointer to surfaceprop2 

	// IDX 1: DepthShadow
	// IDX 2: DepthPrepass
	// IDX 3: DepthVSM
	// IDX 4: DepthShadowTight
	// IDX 5: ColPass

	uint64_t guidRefs[5] <read=Str("%s - %LX", "matl", this)>; // Required to have proper textures.
	uint64_t shadersetGuid <read=Str("%s - %LX", "shds", this)>; // guid of the shaderset asset that this material uses

	RPakPtr pTextureHandles; // TextureGUID Map
	RPakPtr pStreamingTextureHandles; // reserved slot for texture guids which have streamed mip levels
	short streamingTextureCount; // reserved slot for the number of textures with streamed mip levels

	short width;
	short height;

	short unknown1; // reserved texture count?
	
	int flags <read=Str("0x%LX", this)>; // related to cpu data

	int unknown2;
	int unknown3;	
	int unknown4;
	
	__int64 flags2 <read=Str("0x%LX", this)>;
	
	UnknownMaterialSectionV15 unknownSections[2];

	byte bytef0;
	byte bytef1;

	MaterialShaderType_t materialType; // material type

	byte bytef3; // used for unksections loading in UpdateMaterialAsset

	int unk;

    uint64_t textureAnimationGuid <read=Str("%s - %LX", "txan", this)>;
};

// header size unchanged all the way until v20, does not mean vars have not changed within
struct MaterialHeaderV16
{
	__int64 reservedVtbl; // Gets set to CMaterialGlue vtbl ptr
	char padding[8]; // unused

	uint64_t guid <read=Str("%s - %LX", "matl", this)>; // guid of this material asset

	RPakPtr pName <read=ReadString(pageoffsets[this.Index] + this.Offset)>; // pointer to partial asset path
	RPakPtr pSurfaceProp <read=ReadString(pageoffsets[this.Index] + this.Offset)>; // pointer to surfaceprop (as defined in surfaceproperties.txt)
	RPakPtr pSurfaceProp2 <read=ReadString(pageoffsets[this.Index] + this.Offset)>; // pointer to surfaceprop2 

	// IDX 1: DepthShadow
	// IDX 2: DepthPrepass
	// IDX 3: DepthVSM
	// IDX 4: DepthShadowTight
	// IDX 5: ColPass

	uint64_t guidRefs[5] <read=Str("%s - %LX", "matl", this)>; // Required to have proper textures.
	uint64_t shadersetGuid <read=Str("%s - %LX", "shds", this)>; // guid of the shaderset asset that this material uses

	RPakPtr pTextureHandles; // TextureGUID Map
	RPakPtr pStreamingTextureHandles; // reserved slot for texture guids which have streamed mip levels
	short streamingTextureCount; // reserved slot for the number of textures with streamed mip levels

	short width;
	short height;

	short unknown1; // reserved texture count?
	
	int flags <read=Str("0x%LX", this)>; // related to cpu data

	int unknown2;
	int unknown3;	
	int unknown4;
	
	__int64 flags2 <read=Str("0x%LX", this)>;
	
	UnknownMaterialSectionV15 unknownSections;

    int unk_v16[2];

    byte bytef0;
	byte bytef1;

	MaterialShaderType_t materialType; // material type

	byte bytef3; // used for unksections loading in UpdateMaterialAsset

	int unk;

    uint64 textureAnimationGuid <read=Str("%s - %LX", "txan", this)>;

    uint32 unk1_v16[6];
};

struct MaterialHeaderV20
{
	uint64_t unk_v20; // not 
	uint8_t m_Padding[0x8]; // Un-used.
	uint64_t m_nGUID; // guid of this material asset

	RPakPtr m_pszName; // pointer to partial asset path
	RPakPtr m_pszSurfaceProp; // pointer to surfaceprop (as defined in surfaceproperties.rson)
	RPakPtr m_pszSurfaceProp2; // pointer to surfaceprop2 

	// IDX 1: DepthShadow
	// IDX 2: DepthPrepass
	// IDX 3: DepthVSM
	// IDX 4: DepthShadowTight
	// IDX 5: ColPass

	uint64_t m_GUIDRefs[5]; // Required to have proper textures.
	uint64_t m_pShaderSet; // guid of the shaderset asset that this material uses

	RPakPtr m_pTextureHandle; // TextureGUID Map
	RPakPtr m_pStreamingTextureHandles; // Streamable TextureGUID Map
	
	int16 m_nStreamingTextureHandleCount; // Number of textures with streamed mip levels.
	int16 m_nWidth;
	int16 m_nHeight;
	int16 m_Unknown1;
	
	uint32_t m_Flags;
	uint32_t m_Unknown2;
	
	uint32_t m_Unknown3; // REQUIRED but why?
	
	uint32_t m_Unknown4;
	
	// of these 2 seem to be required
	uint32_t m_Flags2;
	uint32_t something2;
	
	UnknownMaterialSectionV15 m_UnknownSections;

    float unkfloat_v20[2];

    uint8_t bytef0;
	uint8_t bytef1;
	MaterialShaderType_t bytef2; // used '4' and '8' observed
	uint8_t bytef3; // used for unksections loading in UpdateMaterialAsset

	uint32_t unk; // seen this as '1' and '3'

    uint64 txanGuid;

    uint32 unk1_v16[5];

    float unkfloat1_v20;
};

struct MaterialCPUHeader
{
	RPakPtr pData; // points to the rest of the cpu data. maybe for colour?
	int size;
	int version; // every unknown is now either datasize, version, or flags
};

struct uvTransformMatrix
{
	// this section is actually broken up into three parts.

	// c_uvRotScaleX
	float uvScaleX;
	float uvRotationX; // rotation, but w e i r d.

	// c_uvRotScaleY
	float uvRotationY; //counter clockwise, 0-1, exceeding one causes Weird Stuff to happen.
	float uvScaleY;

	// c_uvTranslate
	float uvTranslateX;
	float uvTranslateY;
};

// some of these vary so this is just very generic
struct MatlCPUData_V12
{
	// the assignment of these depends on the shader set, they work similarly to texturetransforms in normal source.
	uvTransformMatrix c_uv1; // this is frequently used for detail textures.
	uvTransformMatrix c_uv2;
	uvTransformMatrix c_uv3;

	Vector2 c_uvDistortionIntensity; // distortion on the { x, y } axis.
	Vector2 c_uvDistortion2Intensity; // see above, but for a second distortion texture.

	float c_fogColorFactor;

	float c_layerBlendRamp; // blend intensity (assumed), likely the hardness/softness of the two textures meshing.

	Vector3 c_albedoTint; // color of the albedo texture.
	float c_opacity; // untested.

	float c_useAlphaModulateSpecular;
	float c_alphaEdgeFadeExponent;
	float c_alphaEdgeFadeInner;
	float c_alphaEdgeFadeOuter;

	float c_useAlphaModulateEmissive; // almost always set to 1.
	float c_emissiveEdgeFadeExponent;
	float c_emissiveEdgeFadeInner;
	float c_emissiveEdgeFadeOuter;

	float c_alphaDistanceFadeScale;
	float c_alphaDistanceFadeBias;
	float c_alphaTestReference;

	float c_aspectRatioMulV; // this is equal to width divided by height see: 16/9~, not clear what it actually does.

	Vector3 c_emissiveTint; // color of the emission, this is normally set to { 0.0, 0.0, 0.0 } if you don't have an emission mask.

	float c_shadowBias;

	float c_tsaaDepthAlphaThreshold;
	float c_tsaaMotionAlphaThreshold;
	float c_tsaaMotionAlphaRamp;
	uint32_t c_tsaaResponsiveFlag; // this is 0 or 1 I think.

	float c_dofOpacityLuminanceScale;

	//float c_glitchStrength; // only used  sometimes. on 'Glitch' shadersets, if used 'pad_CBufUberStatic' is only two sections.

	uint32_t pad_CBufUberStatic[3]; // this is reserved space for special values, three sections by default.

	float c_perfGloss;

	Vector3 c_perfSpecColor; // specular color, consistent across most materials.
};

//
// SHADERS
//
enum <byte> ShaderType
{
    Pixel,
    Vertex,
    Geometry,
    Hardware,
    Domain,
    Compute
};

struct ShaderHeaderV8
{
	RPakPtr pName <read=ReadString(pageoffsets[this.Index] + this.Offset)>;

	ShaderType shaderType;
    byte padd[3]; // do this so we can use the same enum
    int dataCount;

	RPakPtr pUnkData;
    RPakPtr pUnkData1;
};

struct ShaderHeaderV12
{
	RPakPtr pName <read=ReadString(pageoffsets[this.Index] + this.Offset)>;

	ShaderType shaderType;
    byte unk[3];

    short unk1[2];

	RPakPtr pUnkData;
    RPakPtr pUnkData1;
};

struct ShaderHeader
{
  RPakPtr pName;
  ShaderType shaderType;
  char m_byte2;
  char m_byte3;
  char m_byte4;
  char m_byte5;
  char m_byte6;
  char m_byte7;
  char m_byte8;
  RPakPtr m_pUnk1;
  RPakPtr m_pUnk2;
};

struct ShaderSetHeader
{
	uint8_t Unknown1[0x18];
	uint16_t Count1;
	uint16_t Count2;
	uint16_t Count3;
	uint8_t Byte1;
	uint8_t Byte2;

	uint8_t Unknown2[0x10];

	uint64_t ShaderHash1;
	uint64_t ShaderHash2;

	uint64_t VertexShaderHash;
	uint64_t PixelShaderHash;
};

struct ShaderSetHeaderV8
{

	__int64 reservedVtbl;
	
    RPakPtr pName <read=ReadString(pageoffsets[this.Index] + this.Offset)>;

	char Unknown1[8];

	short Count1;

	short textureInputCount;

	short Count3;

	byte Byte1;
	byte Byte2;

	byte Unknown2[8];
    
    __int64 unk[4];

    /*uint64_t domainShaderGuid
	uint64_t hullShaderGuid;
	uint64_t computeShaderGuid;
	uint64_t geometryShaderGuid;*/
	uint64_t vextexShaderGuid <read=Str("%s - %LX", "shdr", this)>;
	uint64_t pixelShaderGuid <read=Str("%s - %LX", "shdr", this)>;

};

struct ShaderSetHeaderV11
{

	__int64 reservedVtbl;
	
    RPakPtr pName <read=ReadString(pageoffsets[this.Index] + this.Offset)>;

	char Unknown1[8];

	short Count1;

	short textureInputCount;

	short Count3;

	byte Byte1;
	byte Byte2;

	uint64_t ShaderHash1;
	uint64_t ShaderHash2;

	uint64_t vextexShaderGuid <read=Str("%s - %LX", "shdr", this)>;
	uint64_t pixelShaderGuid <read=Str("%s - %LX", "shdr", this)>;

};

//
// TEXTURES 
//
struct TextureHeader
{
	uint64_t guid <format=hex>;
	RPakPtr pName <read=ReadString(pageoffsets[this.Index] + this.Offset)>;

	uint16_t width <fgcolor=cPurple>;
	uint16_t height <fgcolor=cYellow>;

    // possible int16
    uint16_t Un;
	//uint8_t Un1;
	//uint8_t Un2;

    uint16_t format;  // Maps to a DXGI format

	uint32_t dataSize;	// This is the total amount of image data across all banks

	uint8_t compressionType; // 8 PS4, 9 Switch

	uint8_t optStreamedMipCount; // r5 only

	uint8_t arraySize;
	uint8_t layerCount;

	uint8_t mipFlags; // 0x1 invert mips, 0x2 exists but unk
	uint8_t permanentMipCount;
	uint8_t streamedMipCount;

    byte unk[13]; // mipmap related, used bytes is always total mip count minus one, not present if no mipmaps (see mip count - 1)

    int64 numPixels; // reserved, set on load.
};

struct TextureHeaderV9
{
	RPakPtr pName <fgcolor=cLtRed>;

	uint16_t format;

	uint16_t width <fgcolor=cPurple>;
	uint16_t height <fgcolor=cYellow>;

	uint16_t Un;

	uint16_t arraySize;

    uint16_t un2; // likely layerCount

	uint32_t dataSize_deprecated;

	uint8_t permanentMipCount;
	uint8_t streamedMipCount;
	uint8_t optStreamedMipCount;

	uint8_t un3; // not related to mips, will be present even where there's one mip
	
    int16 unk[10]; // weird mip bytes for unstreamed

    byte unk1[8]; // weird mip level bytes for streamed stuff
};

struct UIIAFlags
{
	uint8_t HasLowTable : 1;
	uint8_t LowTableBc7 : 1;
	uint8_t HasHighTable : 1;
	uint8_t HighTableBc7 : 1;
	// 0x1 = RpakDecompress
	// 0x2 = RpakDecompressSnowflake
	uint8_t CompressionType : 2;
	uint8_t SizeShift : 2;
};

struct UIIAHeader
{
	uint64_t Zero;
	uint64_t NegativeOne;

	uint16_t AtlasCount;

	UIIAFlags Flags;
	uint8_t Padding;
	uint16_t Unknown;
	uint16_t NegativeOne2;

	uint16_t Width;
	uint16_t Height;

	uint32_t Padding2;

	float FloatTable[0x8];
};

struct RUIImage
{
	uint32_t Zero[0x8];

	uint16_t HighResolutionWidth;
	uint16_t HighResolutionHeight;

	uint16_t LowResolutionWidth;
	uint16_t LowResolutionHeight;

	uint32_t BufferIndex;
	uint32_t BufferOffset;

	uint32_t NameIndex;
    uint32_t NameOffset;
    uint32_t Unk[2];
};

//
// UI IMAGE ATLAS
//
struct UIMGHeader
{
	float WidthRatio;
    float HeightRatio;
	uint16_t Width;
	uint16_t Height;
	uint16_t TextureOffsetsCount;
	uint16_t TextureCount;
	uint32_t TextureOffsetsIndex;
	uint32_t TextureOffsetsOffset;
	uint32_t TextureDimsIndex;
	uint32_t TextureDimsOffset;
	uint32_t Unk20;
	uint32_t Unk24;
	uint32_t TextureHashesIndex;
	uint32_t TextureHashesOffset;
	uint32_t TextureNamesIndex;
	uint32_t TextureNamesOffset;
	RPakGuid TextureGuid;
};

struct UIImageOffset
{
	// these don't seem to matter all that much as long as they are a valid float number
	float f0;
	float f1;
	
	// endX and endY define where the edge of the image is, with 1.f being the full length of the image and 0.5f being half of the image
	float endX;
	float endY;

	// startX and startY define where the top left corner is in proportion to the full image dimensions
	float startX;
	float startY;

	// changing these 2 values causes the image to be distorted on each axis
	float unkX;
	float unkY;
};

struct UIImageUV
{
	// maybe the uv coords for top left?
	// just leave these as 0 and it should be fine
	float uv0x;
	float uv0y;

	// these two seem to be the uv coords for the bottom right corner
	// examples:
	// uv1x = 10;
	// | | | | | | | | | |
	// uv1x = 5;
	// | | | | |
	float uv1x;
	float uv1y;
};


//
// MODELS 
//
struct ModelHeaderV8
{
	// IDST data
	RPakPtr pRMDL <comment=".rmdl">;

	// model path
	// e.g. mdl/vehicle/goblin_dropship/goblin_dropship.rmdl
	RPakPtr pName <read=ReadString(pageoffsets[this.Index] + this.Offset)>;

	uint64_t unk;

	// charms do not use this, but goblin_dropship and weapons do
	RPakPtr pPHY <comment=".phy">;

	RPakPtr pModelData <comment=".vtx and .vvd. preload data for static props only">;

	// pointer to data for the model's arig guid(s?)
	RPakPtr pRRIG <comment="pointer to the guids of the anim rigs associated with this model">;

	// this is a guess based on the above ptr's data. i think this is == to the number of guids at where the ptr points to
	uint32_t rrigCount;

	// size of the data kept in starpak
	uint32_t unkDataSize;
	uint32_t or ; // full size of the starpak entry, aligned to 4096.

	// number of anim sequences directly associated with this model
	uint32_t rseqCount;
	RPakPtr pRSEQ;

    uint64_t unk1;
};

struct ModelHeaderV9
{
	// IDST data
	RPakPtr pRMDL <comment=".rmdl">;

    uint64_t unk;

	// model path
	// e.g. mdl/vehicle/goblin_dropship/goblin_dropship.rmdl
	RPakPtr pName <read=ReadString(pageoffsets[this.Index] + this.Offset)>;

	uint64_t unk1;

	// charms do not use this, but goblin_dropship and weapons do
	RPakPtr pPHY <comment=".phy">;

    uint64_t unk2;

	RPakPtr pModelData <comment=".vtx and .vvd combined. preload data for static props only">;

	// pointer to data for the model's arig guid(s?)
	RPakPtr pRRIG <comment="pointer to the guids of the anim rigs associated with this model">;

	// this is a guess based on the above ptr's data. i think this is == to the number of guids at where the ptr points to
	uint32_t rrigCount;

	// size of the data kept in starpak
	uint32_t unkDataSize;
	uint32_t alignedStreamingSize; // full size of the starpak entry, aligned to 4096.	

    uint64_t unk3;
    uint64_t unk4;
	uint64_t unk5;

    // number of anim sequences directly associated with this model
	uint32_t rseqCount;
	RPakPtr pRSEQ <comment="pointer to the guids of the rseqs associated with this model">;

	uint64_t unk6;
};

// season 7
struct ModelHeaderV121
{
	// IDST data
	RPakPtr pRMDL <comment=".rmdl">;

    uint64_t unk;

	// model path
	// e.g. mdl/vehicle/goblin_dropship/goblin_dropship.rmdl
	RPakPtr pName <read=ReadString(pageoffsets[this.Index] + this.Offset)>;

	uint64_t unk1;

	// charms do not use this, but goblin_dropship and weapons do
	RPakPtr pPHY <comment=".phy">;

    uint64_t unk2;

	RPakPtr pModelData <comment=".vtx and .vvd combined. preload data for static props only">;

	// pointer to data for the model's arig guid(s?)
	RPakPtr pRRIG <comment="pointer to the guids of the anim rigs associated with this model">;

	// this is a guess based on the above ptr's data. i think this is == to the number of guids at where the ptr points to
	uint32_t rrigCount;

	// size of the data kept in starpak
	uint32_t unkDataSize;
	uint32_t alignedStreamingSize; // full size of the starpak entry, aligned to 4096.
	
    uint64_t unk3;

    // number of anim sequences directly associated with this model
	uint32_t rseqCount;
	RPakPtr pRSEQ <comment="pointer to the guids of the rseqs associated with this model">;

	uint64_t unk6;
};

struct ModelHeaderV13
{
	// IDST data
	RPakPtr pRMDL <comment=".rmdl">;

    uint64_t unk;

	// model path
	// e.g. mdl/vehicle/goblin_dropship/goblin_dropship.rmdl
	RPakPtr pName <read=ReadString(pageoffsets[this.Index] + this.Offset)>;

	uint64_t unk1;

	// charms do not use this, but goblin_dropship and weapons do
	RPakPtr pPHY <comment=".phy">;

    uint64_t unk2;

	RPakPtr pModelData <comment=".vtx and .vvd combined. preload data for static props only">;

	// pointer to data for the model's arig guid(s?)
	RPakPtr pRRIG <comment="pointer to the guids of the anim rigs associated with this model">;

	// this is a guess based on the above ptr's data. i think this is == to the number of guids at where the ptr points to
	uint32_t rrigCount;

	// size of the data kept in starpak
	uint32_t unkDataSize;
	uint32_t alignedStreamingSize; // full size of the starpak entry, aligned to 4096.	

    Vector3 unkvec; // mins
    Vector3 unkvec1; // maxs

    uint64_t unk3;

    // number of anim sequences directly associated with this model
	uint32_t rseqCount;
	RPakPtr pRSEQ <comment="pointer to the guids of the rseqs associated with this model">;

	uint64_t unk6;
};

struct AnimRigHeaderV4
{
	RPakPtr pRRIG;
    RPakPtr pName;

	int unk;

	int rseqCount;
	RPakPtr pRSEQ <comment="pointer to the guids of the rseqs associated with this model">;

	int64 unk1;
};

struct UIFontData
{
	RPakPtr Ptr1;
	uint64_t Unk1[5];
	RPakPtr Ptr2;
	RPakPtr Ptr3;
	RPakPtr Ptr4;
	RPakPtr Ptr5;
	RPakPtr Ptr6;
};

// size: 40 (0x28)
struct UIFontHeader
{
	uint16_t FontCount;
	uint16_t Unk2;
	uint16_t Width;
	uint16_t Height;
	uint32_t Unk3;
	uint32_t Unk4;
	RPakPtr FontData;
	RPakPtr Ptr2;
	uint64_t AtlasGUID;
};

struct RSONHeader
{
	int type<format=hex>;
	int nodeCount;
	RPakPtr pNodes;
};

struct RSONNode
{
    RPakPtr pName;
    int type;
    int valueCount;
    RPakPtr pValues;
};


// no

enum <char> RuiArgumentType_t
{
  TYPE_NONE = 0,
  TYPE_STRING = 0x1,
  TYPE_ASSET = 0x2,
  TYPE_BOOL = 0x3,
  TYPE_INT = 0x4,
  TYPE_FLOAT = 0x5,
  TYPE_FLOAT2 = 0x6,
  TYPE_FLOAT3 = 0x7,
  TYPE_COLOR_ALPHA = 0x8,
  TYPE_GAMETIME = 0x9,
  TYPE_WALLTIME = 0xA,
  TYPE_UIHANDLE = 0xB,
  TYPE_IMAGE = 0xC,
  TYPE_FONT_FACE = 0xD,
  TYPE_FONT_HASH = 0xE,
  TYPE_ARRAY = 0xF,
};

struct RUIHeader
{
	RPakPtr name;
	RPakPtr values;
	RPakPtr unk2;
	float elementWidth;
	float elementHeight;
	float elementWidthRatio <comment=Str("1/%f", parentof(this).elementWidth)>;
    float elementHeightRatio <comment=Str("1/%f", parentof(this).elementHeight)>;
	RPakPtr argNames;
	RPakPtr argClusters;
	RPakPtr args;
	short argCount; // number of slots for arguments. not all are used
	short unk10Count; // number of "headers" unk10 leads to, if 0 none present.
	uint16_t unk4;
    uint16_t valueSize; // size of values in bytes
	uint16_t unk5;
	uint16_t unk6;
	uint16_t unk7;
	uint16_t argClusterCount;
	RPakPtr unk8;
	RPakPtr unk9;
	RPakPtr unk10; // leads to "headers" for data, the data being vectors
};

struct RuiArgCluster
{
  uint16_t argIndex;
  uint16_t argCount;
  char byte1; // hash_mul
  char byte2; // hash_add
};

struct RuiArg
{
    RuiArgumentType_t type;
    char unk1;
    short valueOffset;
    short nameOffset;
    short short_hash;
};

struct RuiUnk10
{
    uint32_t dataCount; // number of vectors the ptr leads to.
    uint16_t unk1;
    uint16_t unk2;

    RPakPtr data;
};

struct TextureListHeader_t
{
	RPakPtr textureGuids;
	RPakPtr textureNames;
	uint64_t textureCount;
};




struct ParticleScriptHeader
{
    uint64_t m_pTextures[4];
    char unk[0x20];
    RPakPtr m_pszName;
    RPakPtr m_pUnk2;
    uint64_t m_pSysEffectComputeShader; // seems to only be used on particle_script/system.rpak
    uint64_t m_pProcessComputeShader;
    uint64_t m_pRenderVertexShader;
    uint64_t m_pRenderGeometryShader;
    uint64_t m_pRenderPixelShader;
    uint32_t unk2;
    uint32_t unk3;
    uint64_t unk4;
};

struct SettingsLayoutHeader
{
    RPakPtr m_pszName;
    RPakPtr m_pUnk1;
    RPakPtr m_pUnk2;
    uint32_t dwUnk1;
    uint32_t dwUnk2;
    uint32_t dwUnk3;
    uint32_t dwUnk4;
    uint32_t dwUnk5;
    uint32_t dwUnk6;
    uint32_t dwUnk7;
    uint32_t dwUnk8;
    RPakPtr m_pUnk3;
    RPakPtr m_pUnk4;
}; // size = 72

struct SettingsAssetHeader
{
  QWORD layoutGuid<format=hex>;
  RPakPtr pValues;
  RPakPtr pName;
  RPakPtr pStringBuffer;

  uint32_t guid; // actually 31 bit :D
  uint32_t unk1;

  RPakPtr pModNames; // e.g. passives for settings/mp/player/pilot_survival.rpak

  RPakPtr unk2;
  uint32_t valueBufSize;
  char unk[12];
}; // size = 72


// aseq headers
struct ASeqHeaderV7
{
	RPakPtr pData; // pointer to raw rseq.
	RPakPtr pName <read=ReadString(pageoffsets[this.Index] + this.Offset)>; // pointer to debug name, placed before raw rseq normally.

	// this can point to a group of guids and not one singular one.
	RPakPtr pModels;
	int modelCount;

    int reserved;

	// this can point to a group of guids and not one singular one.
	RPakPtr pSettings;
	int settingsCount;

    int reserved1;
};

struct ASeqHeaderV71
{
    RPakPtr pData; // pointer to raw rseq.
    RPakPtr pName <read=ReadString(pageoffsets[this.Index] + this.Offset)>; // pointer to debug name, placed before raw rseq normally.

	// this can point to a group of guids and not one singular one.
	RPakPtr pModels;
	int modelCount; // to be verified on v7

	// size of data that is not locally in the rseq.
	int externalDataSize;

	// this can point to a group of guids and not one singular one.
	RPakPtr pSettings;
	int settingsCount;

    int reserved1; // assumed

	// pointer to data stored outside of the raw rseq.
    RPakPtr pExternalData;
};

struct ASeqHeaderV10
{
	RPakPtr pData; // pointer to raw rseq.
	RPakPtr pName <read=ReadString(pageoffsets[this.Index] + this.Offset)>; // pointer to debug name, placed before raw rseq normally.

    __int64 unknown; // possible pointer, guid, or reserved space.

	// counts for mdl_ and stgs assets, normally just one but can be multiples.
    short modelCount;
    short settingsCount;

	// size of the external data.
    int externalDataSize;

	// these can all point to a group of guids and not one singular one.
    RPakPtr pModels;
	RPakPtr pEffects;
    RPakPtr pSettings;

	// data that is stored outside of the raw rseq.
    RPakPtr pExternalData;
};

struct SubtitleHeader
{
    uint32 stringCount;
    uint32 unk2;
    RPakPtr unk3;
    RPakPtr pEntries;
};


struct SubtHashEntry {
    uint hash;
    uint stringoffset;
};

/*struct Vector2
{
    float x,y;
};*/


// 816 bytes...
struct AnimRecordingHeader
{
    // pose param names normally
    RPakPtr keys[15] <read=ReadString(pageoffsets[this.Index] + this.Offset)>;

    // pose param values normally
    Vector2 values[15];
    
    RPakPtr anims[48] <read=ReadString(pageoffsets[this.Index] + this.Offset)>;

    // actions or events?
    RPakPtr unkstring[16] <read=ReadString(pageoffsets[this.Index] + this.Offset)>;

    Vector2 unkvector[3];

    RPakPtr unkdata;
    RPakPtr unkdata1;

    int unkdatacount;
    int unkdatacount1;

    int64 unk;

    byte unk1[4];

    int unk2;
};

struct unkdata_t
{
    float unkfloat[7];

    byte unk[8];

    float unkfloat1[4];

    byte unk1[16];
};

struct unkdata1_t
{
    int unk[2];

    float unkfloat[7];
};

struct MaterialForAspect
{
    RPakGuid rgdu;
    RPakGuid rgdp;
    RPakGuid sknu;
    RPakGuid sknp;
    RPakGuid sknc;
    RPakGuid wldu;
    RPakGuid ptcu;
    RPakGuid ptcs;
};

struct PtchHeader
{
	uint32_t unknown_1; // always FF 00 00 00?
	uint32_t patchedPakCount;

	RPakPtr pPakNames;

	RPakPtr pPakPatchNums;
};

// oh god oh man oh god
struct MapHeader
{
    char unk[104];

    if(AssetEntries[i].Version == 3)
        char unk_v3[16];
};

struct EffectHeader
{
	RPakPtr data;
	int unk2;
	int unk3;
};

struct EffectData
{
	RPakPtr pcfName;
	RPakPtr EffectName; // Double ptr to effect name
	RPakPtr unk2;
	RPakPtr ParticleSystemOperator; // Double ptr to pso name
	RPakPtr unk3;
	RPakPtr unk4;
};